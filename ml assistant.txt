10 rem ml assistant by cjs 2019-09
90 gosub 800
100 rem command list
110 cs$="9fedgzq?"
120 print cs$+"{reverse on}>{reverse off} ";
200 get c$
210 if c$="" then goto 200
220 print c$;" ";
230 found=0
240 for i=1 to len(cs$)
250 if c$=mid$(cs$,i,1) then found=1
260 next i
270 if found=0 then print "{reverse on}err{reverse off}":goto 100
300 if c$="9" then gosub 1000
310 if c$="f" then gosub 2000
320 if c$="e" then gosub 3000
330 if c$="d" then gosub 4000
340 if c$="g" then gosub 5000
350 if c$="z" then print:print  "  ";:gosub 10000
360 if c$="q" then end
370 if c$="?" then print:gosub 800
399 goto 100
800 rem menu/help text
810 print "----------------------------------------";
820 print "****** machine language assistant ******";
830 print "  hex input requests marked with '$'."
840 print "9) decimal-> hex    f) hex->decimal"
850 print "e) examine 16 bytes d) deposit"
860 print "g) goto (sys)       z) print err status"
870 print "q) quit             ?) print this menu"
899 return
1000 rem decimal to hex command
1010 print "dec";:input in
1020 gosub 1100
1030 print " $";out$
1040 return
1100 rem decimal to hex subroutine
1110 rem in=input out$=output
1120 rem t*=temp vars
1130 out$="":t0=in
1140 t1=(t0/16-int(t0/16))*16
1145 t0=int(t0/16)
1150 if t1 > 9 then t1=t1+7
1160 out$=chr$(t1+48)+out$
1170 if t0>0 then goto 1140
1190 return
2000 rem hex to decimal command
2010 print "$hex";:input in$
2020 gosub 2100
2025 if out<0 then return
2030 print " ";out
2040 return
2100 rem hex to decimal subroutine
2110 rem in$=input out=output
2120 rem t*=temp vars
2130 out=0
2140 for t0=1 to len(in$)
2150 out=out*16
2160 t1=asc(mid$(in$,t0,1))
2170 if t1<48 or t1>70 then print "{reverse on}err{reverse off}":out=-1:return
2180 if t1>58 and t1<65 then print "err":out=-1:return
2190 if t1<58 then out=out+t1-48
2200 if t1>=65 then out=out+t1-65+10
2210 next t0
2220 return
3000 rem examine memory command
3005 rem e*=temp vars
3010 input "$addr";in$
3020 gosub 2100
3025 el=16:rem length to dump
3030 for ei=out to out+el-1
3035 if ei>out and ei/8=int(ei/8) then print
3040 in=peek(ei)
3050 gosub 1100
3055 print " ";
3060 if len(out$)<2 then print "0";
3070 print out$;
3080 next ei
3090 print:return
4000 rem deposit
4010 rem d*=temp vars
4020 input "$addr";in$:gosub 2100:da=out
4030 print "enter to skip, . to exit"
4100 in=da:gosub 1100:rem addr to hex
4110 print out$;":";
4120 in=peek(da):gosub 1100:rem value to hex
4130 print out$;
4135 in$=""
4140 input " $";in$
4145 if in$="" then goto 4200
4150 if in$="." then return
4160 gosub 2100:rem new value to decimal
4165 if out=-1 then goto 4100
4170 poke da,out
4200 da=da+1
4210 goto 4100
4999 return
5000 rem goto (sys) command
5010 input "$addr";in$:gosub 2100
5020 sys out
5030 return
10000 rem show current disk error
10010 rem use when you see green blinking light on drive
10020 open 15,8,15
10030 input# 15,en,en$,et,es
10035 close 15
10040 print "err";en;en$;"  t/s";et;es
10060 return
